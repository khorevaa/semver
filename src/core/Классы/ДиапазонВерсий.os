#Использовать logos

Перем Лог;

Перем ЛевоеСравнение Экспорт;
Перем ПравоеСравнение Экспорт;

Перем ДоступныеСимволы;

Процедура ПриСозданииОбъекта(Знач ДиапазонСтрокой)

	ДоступныеСимволы = Версии.ДоступныеСимволы();

	ПреобразоватьВСравнение(ДиапазонСтрокой);

КонецПроцедуры

Функция ПроверитьВерсию(Знач ПроверяемаяВерсия) Экспорт

	ПроверкаНаБольше = ВыполнитьСравнения(ПроверяемаяВерсия, ЛевоеСравнение);
	Лог.Отладка("Проверка на больше: %3 (%2) %1 = %4", ЛевоеСравнение.Версия.ВСтроку(), ЛевоеСравнение.Знак, ПроверяемаяВерсия.ВСтроку(), ПроверкаНаБольше);
	Если Не ПроверкаНаБольше Тогда
		Возврат Ложь;
	КонецЕсли;

	Если Не ПравоеСравнение = Неопределено Тогда

		ПроверкаНаМеньше = ВыполнитьСравнения(ПроверяемаяВерсия, ПравоеСравнение);
		Лог.Отладка("Проверка на меньше: %3 (%2) %1 = %4", ПравоеСравнение.Версия.ВСтроку(), ПравоеСравнение.Знак, ПроверяемаяВерсия.ВСтроку(), ПроверкаНаМеньше);
		
		Возврат ПроверкаНаМеньше;

	КонецЕсли;

	Возврат Истина;

КонецФункции

Функция ВыполнитьСравнения(Знач ВерсияСравнения, Знач Сравнение)

	//Лог.Отладка("Выполняю сравнение: %3 (%2) %1", Сравнение.Версия.ВСтроку(), Сравнение.Знак, ВерсияСравнения.ВСтроку());

	Если Сравнение.Знак = "=" Тогда

		Возврат ВерсияСравнения.Равны(Сравнение.Версия);

	ИначеЕсли Сравнение.Знак = ">=" Тогда

		Возврат ВерсияСравнения.БольшеИлиРавны(Сравнение.Версия);

	ИначеЕсли Сравнение.Знак = ">" Тогда

		Возврат ВерсияСравнения.Больше(Сравнение.Версия);

	ИначеЕсли Сравнение.Знак = "<=" Тогда

		Возврат ВерсияСравнения.МеньшеИлиРавны(Сравнение.Версия);

	ИначеЕсли Сравнение.Знак = "<" Тогда

		Возврат ВерсияСравнения.Меньше(Сравнение.Версия);

	КонецЕсли;

КонецФункции

Функция ПроверитьВерсии(Знач МассивВерсий) Экспорт

	ИтоговыйМассив = Новый Массив;

	//Сообщить(МассивВерсий.Количество());

	Для каждого ПроверяемаяВерсия Из МассивВерсий Цикл

		Если ПроверитьВерсию(ПроверяемаяВерсия) Тогда
			ИтоговыйМассив.Добавить(ПроверяемаяВерсия);
		КонецЕсли;

	КонецЦикла;

	Возврат ИтоговыйМассив;

КонецФункции

Функция МаксимальнаяВерсияИЗВерсий(Знач ПроверяемыйМассивВерсий) Экспорт

	//Сообщить(ПроверяемыйМассивВерсий.Количество());

	ИтоговыйМассив = ПроверитьВерсии(ПроверяемыйМассивВерсий);
	Лог.Отладка("МаксимальнаяВерсияИЗВерсий >> версий в массиве: %1", ИтоговыйМассив.Количество());

	Возврат Версии.МаксимальнаяИзМассива(ИтоговыйМассив);

КонецФункции

Функция ВСтроку() Экспорт

КонецФункции

Процедура ПреобразоватьВСравнение(Знач ДиапазонСтрокой)

	ПростоеСравнение = Версии.ЭтоПростоеСравнение(ДиапазонСтрокой);

	Если ПростоеСравнение Тогда
		ОбработатьПростоеСравнение(ДиапазонСтрокой);
		Возврат;
	КонецЕсли;

	ЭтоТильда = СтрНачинаетсяС(ДиапазонСтрокой, ДоступныеСимволы.Тильда);

	Если ЭтоТильда Тогда
		ОбработатьСравнениеТильда(ДиапазонСтрокой);
		Возврат;
	КонецЕсли;

	ЭтоКаретка = СтрНачинаетсяС(ДиапазонСтрокой, ДоступныеСимволы.Каретка);

	Если ЭтоКаретка Тогда
		ОбработатьСравнениеКаретка(ДиапазонСтрокой);
		Возврат;
	КонецЕсли;

	ЛюбойСимвол = ЭтоЛюбойСимвол(ДиапазонСтрокой);

	Если ЛюбойСимвол Тогда
		ОбработатьЛюбойСимвол(ДиапазонСтрокой);
		Возврат;
	КонецЕсли;

КонецПроцедуры

Функция ЭтоЛюбойСимвол(Знач ДиапазонСтрокой)

	МассивСимволовЛюбойСимвол = СтрРазделить(ДоступныеСимволы.ЛюбойСимвол, " ");

	Для каждого ЭлементМассива Из МассивСимволовЛюбойСимвол Цикл

		НашлиСимвол = СтрНайти(ДиапазонСтрокой, ЭлементМассива);

		Если Не НашлиСимвол = Неопределено Тогда

			Возврат Истина;

		КонецЕсли;

	КонецЦикла;

	Возврат Ложь;
КонецФункции

Процедура ОбработатьПростоеСравнение(Знач ДиапазонСтрокой)

	Знак = "=";

	МассивСимволов = СтрРазделить(ДоступныеСимволы.СимволыПростогоСравнения, " ");

	Для каждого ЭлементМассива Из МассивСимволов Цикл

		Если СтрНачинаетсяС(ДиапазонСтрокой, ЭлементМассива) Тогда


			Знак = Сред(ДиапазонСтрокой, 1, СтрДлина(ЭлементМассива));

			Прервать;

		КонецЕсли;

	КонецЦикла;

	ВерсияСравнения = Новый Версия(СтрЗаменить(ДиапазонСтрокой, Знак, ""));

	Лог.Отладка("Левая версия: %1 знак: %2", ВерсияСравнения.ВСтроку(), Знак);

	ЛевоеСравнение = НовоеСравнение(ВерсияСравнения, Знак);
	ПравоеСравнение = Неопределено;

КонецПроцедуры

Процедура ОбработатьСравнениеТильда(Знач ВерсияДиапазона)

	Перем ЛеваяВерсия, ПраваяВерсия, ЛевыйЗнак, ПравыйЗнач;

	ЛевыйЗнак = ">=";
	ПравыйЗнак = "<";

	СтрокаВерсииБезТильды = Сред(ВерсияДиапазона, 2);

	Диапазон = Новый ДиапазонВерсий(СтрокаВерсииБезТильды);

	ЛеваяВерсия = Диапазон.ЛевоеСравнение.Версия;
	ПредварительнаяПраваяВерсия = Новый Версия(ЛеваяВерсия.ВСтроку());

	Если Не Диапазон.ПравоеСравнение = Неопределено Тогда
		ПредварительнаяПраваяВерсия = Диапазон.ПравоеСравнение.Версия;
	КонецЕсли;

	Если Не ЛеваяВерсия.Основная = 0 Тогда
		ПраваяВерсия = Версии.УвеличитьОсновнуюВерсию(ПредварительнаяПраваяВерсия);
	ИначеЕсли Не ЛеваяВерсия.Основная = 0
		и НЕ ЛеваяВерсия.Второстепенная = 0  Тогда
		ПраваяВерсия = Версии.УвеличитьВторостепеннуюВерсию(ПредварительнаяПраваяВерсия);
	ИначеЕсли ЛеваяВерсия.Основная = 0
		И НЕ ЛеваяВерсия.Второстепенная = 0 Тогда
		ПраваяВерсия = Версии.УвеличитьВторостепеннуюВерсию(ПредварительнаяПраваяВерсия);
	ИначеЕсли ЛеваяВерсия.Основная = 0
		И ЛеваяВерсия.Второстепенная = 0 Тогда
		ПраваяВерсия = Версии.УвеличитьВторостепеннуюВерсию(ПредварительнаяПраваяВерсия);
	Иначе
		ПраваяВерсия = Версии.УвеличитьОсновнуюВерсию(ПредварительнаяПраваяВерсия);
	КонецЕсли;


	Лог.Отладка("Левая версия: " + ЛеваяВерсия.ВСтроку());
	Лог.Отладка("Правая версия: " + ПраваяВерсия.ВСтроку());

	ЛевоеСравнение = НовоеСравнение(ЛеваяВерсия, ЛевыйЗнак);
	ПравоеСравнение = НовоеСравнение(ПраваяВерсия, ПравыйЗнак);

КонецПроцедуры

Процедура ОбработатьЛюбойСимвол(Знач ВерсияДиапазона)

	Перем ЛеваяВерсия, ПраваяВерсия, ЛевыйЗнак, ПравыйЗнач;

	ЛевыйЗнак = ">=";
	ПравыйЗнак = "<";

	Если ВерсияДиапазона = "*" Тогда

		ЛевоеСравнение = НовоеСравнение(Новый Версия("0.0.0"), ЛевыйЗнак);

		Возврат;

	КонецЕсли;

	МассивСимволов = СтрРазделить(ДоступныеСимволы.ЛюбойСимвол, " ");

	МассивВерсии = СтрРазделить(ВерсияДиапазона, ".");

	МассивНовойВерсии  = Новый Массив;

	ИндексЛюбогоСимвола = 4;
	ИИ = 0;
	Для Каждого СимволМассива Из МассивВерсии Цикл

		Если Не МассивСимволов.Найти(СимволМассива) = Неопределено Тогда
			ИндексЛюбогоСимвола = ?(ИИ < ИндексЛюбогоСимвола, ИИ, ИндексЛюбогоСимвола);
			МассивНовойВерсии.Добавить(0);
		Иначе
			МассивНовойВерсии.Добавить(СимволМассива);
		КонецЕсли;
		ИИ = ИИ + 1;
	КонецЦикла;

	Если ИндексЛюбогоСимвола = 4 Тогда
		ЛевоеСравнение = НовоеСравнение(Новый Версия(ВерсияДиапазона), ЛевыйЗнак);
		Возврат;
	КонецЕсли;

	СтрокаЛевойВерсии = СтрСоединить(МассивНовойВерсии, ".");
	Лог.Отладка("ОбработатьЛюбойСимвол: Собрали версию: %1", СтрокаЛевойВерсии);
	Лог.Отладка("ОбработатьЛюбойСимвол: Индекс первого любого символа: %1", ИндексЛюбогоСимвола);
	
	ЛеваяВерсия = Новый Версия(СтрокаЛевойВерсии);
	ПраваяВерсия = Новый Версия(ЛеваяВерсия.ВСтроку());
	
	Если ИндексЛюбогоСимвола = 1 Тогда
		ПраваяВерсия = Версии.УвеличитьОсновнуюВерсию(ПраваяВерсия);
	ИначеЕсли ИндексЛюбогоСимвола = 2 Тогда
		ПраваяВерсия = Версии.УвеличитьВторостепеннуюВерсию(ПраваяВерсия);
	// ИначеЕсли Не ЛеваяВерсия.ВерсияПатча = 0 Тогда
	// 	ПраваяВерсия = Версии.УвеличитьВерсиюПатча(ЛеваяВерсия);
	КонецЕсли;

	Лог.Отладка("Левая версия: " + ЛеваяВерсия.ВСтроку());
	Лог.Отладка("Правая версия: " + ПраваяВерсия.ВСтроку());

	ЛевоеСравнение = НовоеСравнение(ЛеваяВерсия, ЛевыйЗнак);
	ПравоеСравнение = НовоеСравнение(ПраваяВерсия, ПравыйЗнак);

КонецПроцедуры

Процедура ОбработатьСравнениеКаретка(Знач ВерсияДиапазона)

	Перем ЛеваяВерсия, ПраваяВерсия, ЛевыйЗнак, ПравыйЗнач;

	ЛевыйЗнак = ">=";
	ПравыйЗнак = "<";

	СтрокаВерсииБезКаретки = Сред(ВерсияДиапазона, 2);

	Диапазон = Новый ДиапазонВерсий(СтрокаВерсииБезКаретки);

	ЛеваяВерсия = Диапазон.ЛевоеСравнение.Версия;

	ПраваяВерсия = Новый Версия(ЛеваяВерсия.ВСтроку());

	Если НЕ Диапазон.ПравоеСравнение = Неопределено Тогда
		
		ПраваяВерсия = Диапазон.ПравоеСравнение.Версия;

	КонецЕсли;

	Если ЛеваяВерсия.Равны(Новый Версия("0.0.0"))
		И ПраваяВерсия.Равны(Новый Версия("0.0.0")) Тогда
		ПраваяВерсия = Версии.УвеличитьВторостепеннуюВерсию(ПраваяВерсия);
	ИначеЕсли Не ЛеваяВерсия.Основная = 0 Тогда
		Если НЕ Версии.УвеличитьОсновнуюВерсию(Новый Версия(ЛеваяВерсия.ВСтроку())).Равны(ПраваяВерсия) Тогда
	
			ПраваяВерсия = Версии.УвеличитьОсновнуюВерсию(ПраваяВерсия);
				
		КонецЕсли;

	ИначеЕсли Не ЛеваяВерсия.Второстепенная = 0 Тогда
		ПраваяВерсия = Версии.УвеличитьВторостепеннуюВерсию(ПраваяВерсия);
	ИначеЕсли Не ЛеваяВерсия.Патч = 0 Тогда
		ПраваяВерсия = Версии.УвеличитьВерсиюПатча(ПраваяВерсия);
	КонецЕсли;

	Если ЛеваяВерсия.Равны(Новый Версия("0.0.0"))
		И ЛеваяВерсия.Равны(ПраваяВерсия) Тогда
		ПраваяВерсия = Версии.УвеличитьВторостепеннуюВерсию(ПраваяВерсия);
	КонецЕсли;

	Лог.Отладка("Левая версия: " + ЛеваяВерсия.ВСтроку());
	Лог.Отладка("Правая версия: " + ПраваяВерсия.ВСтроку());

	ЛевоеСравнение = НовоеСравнение(ЛеваяВерсия, ЛевыйЗнак);
	ПравоеСравнение = НовоеСравнение(ПраваяВерсия, ПравыйЗнак);

КонецПроцедуры

Функция НовоеСравнение(Знач ВерсияСравнения, Знач ЗнакСравнения)

	Возврат Новый Структура("Версия, Знак", ВерсияСравнения, ЗнакСравнения);

КонецФункции


Лог = Логирование.ПолучитьЛог("oscript.lib.semver");
